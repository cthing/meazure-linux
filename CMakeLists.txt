cmake_minimum_required(VERSION 3.24 FATAL_ERROR)

project(meazure_linux
        VERSION 5.0.0.1
        DESCRIPTION "A tool for easily measuring and capturing portions of the screen."
        HOMEPAGE_URL "https://github.com/cthing/meazure-linux")

if(NOT CMAKE_HOST_SYSTEM_NAME MATCHES "Linux")
    message(FATAL_ERROR "Meazure only builds on Linux")
endif()

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

set(APP_DIR "${PROJECT_SOURCE_DIR}/meazure")
set(TEST_DIR "${PROJECT_SOURCE_DIR}/test")
set(DEV_DIR "${PROJECT_SOURCE_DIR}/dev")
set(DOCS_DIR "${PROJECT_SOURCE_DIR}/docs")

include(${DEV_DIR}/conan/cthing-conan.cmake)

conan_cmake_configure(REQUIRES
                      qt/6.4.2
                      libffi/3.4.3   # Resolve Qt conflict between wayland and glib even though wayland is excluded
                      xerces-c/3.2.4
                      OPTIONS qt:shared=False qt:qtsvg=True qt:qttools=True qt:qtwayland=False harfbuzz:with_glib=False xerces-c:char_type=char16_t
                      GENERATORS ${CTHING_CONAN_GENERATOR})

CTHING_CONAN_INSTALL()

find_package(Qt6 COMPONENTS Core Gui Widgets Svg Network Test REQUIRED)
find_package(ICU COMPONENTS uc REQUIRED)
find_package(XercesC REQUIRED)
find_package(Doxygen REQUIRED)
find_program(CMAKE_LINT_PROGRAM "cmake-lint" REQUIRED)

qt_standard_project_setup()

enable_testing()

set(CMAKE_COMPILE_WARNING_AS_ERROR TRUE)
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra -Wno-comment")
set(CMAKE_CXX_CLANG_TIDY clang-tidy-15)

message(STATUS "Build type: ${CMAKE_BUILD_TYPE}")
message(STATUS "Output directory: ${CMAKE_BINARY_DIR}")

file(GLOB_RECURSE BUILD_FILES CMakeLists.txt)
add_custom_target(cmakelint
                  COMMAND ${CMAKE_LINT_PROGRAM} ${BUILD_FILES} --config ${DEV_DIR}/cmake-lint/config.py
                  WORKING_DIRECTORY ${PROJECT_SOURCE_DIR})

add_custom_target(apidocs
                  COMMAND ${CMAKE_COMMAND} -E rm -rf "${CMAKE_CURRENT_BINARY_DIR}/meazure/apidocs"
                  COMMAND ${CMAKE_COMMAND} -E env MEA_APIDOCS_DIR=${CMAKE_CURRENT_BINARY_DIR}/meazure/apidocs ${DOXYGEN_EXECUTABLE} "${DEV_DIR}/doxygen/meazure.cfg"
                  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR})

add_subdirectory(meazure)
add_subdirectory(test)

# Files generated by MOC, RCC, and UIC may produce clang-tidy warnings. Generate a dummy .clang-tidy file in the
# binary directory that disables all clang-tidy checks except one that will never match. This one check is needed
# because clang-tidy reports an error if no checks are enabled. Since the Qt code generators will generate source
# files in the binary tree, clang-tidy will load the configuration from this dummy file when the sources are built.
file(WRITE "${CMAKE_BINARY_DIR}/.clang-tidy" "
---
InheritParentConfig: false
Checks: '-*,llvm-twine-local'
...
")
